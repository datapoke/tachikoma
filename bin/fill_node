#!/usr/bin/env perl
use strict;
use warnings;
use Tachikoma::EventFrameworks::Select;
use Tachikoma::Nodes::Router;
use Tachikoma::Nodes::Tail;
use Tachikoma::Nodes::Shutdown;
use Tachikoma::Nodes::Socket qw( TK_R TK_SYNC );
use Getopt::Long             qw( GetOptions );

use version; our $VERSION = qv('v2.0.929');

my $home            = ( getpwuid $< )[7];
my @default_configs = (
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);
my $config_file = undef;
my $host        = 'localhost';
my $port        = 4230;
my $stream      = undef;
my $throttle    = 1;
my $debug       = undef;
my $help        = undef;

my $r = GetOptions(
    'config=s'   => \$config_file,
    'host=s'     => \$host,
    'port=i'     => \$port,
    'stream=s'   => \$stream,
    'throttle:i' => \$throttle,
    'debug:i'    => \$debug,
    'help'       => \$help,
);
my $to = $ARGV[0];
$debug = 1 if ( defined $debug and not $debug );

usage() if ( $help or not $r or not length $to );
my $config = Tachikoma->configuration;
$config->load_config_file( $config_file ? $config_file : @default_configs );
Tachikoma->event_framework( Tachikoma::EventFrameworks::Select->new );

my $router   = Tachikoma::Nodes::Router->new;
my $tail     = Tachikoma::Nodes::Tail->filehandle( *STDIN, TK_R );
my $shutdown = Tachikoma::Nodes::Shutdown->new;
my $tachikoma =
    Tachikoma::Nodes::Socket->inet_client( $host, $port, TK_SYNC );
$tail->name('_stdin');
$tail->buffer_mode('line-buffered');
$tail->max_unanswered($throttle);
$tail->stream($stream);
$tail->debug_state($debug);
$tail->on_EOF('shutdown');
$tail->owner($to);
$tail->sink($shutdown);
$shutdown->sink($tachikoma);
$tachikoma->debug_state($debug);
$tachikoma->on_EOF('die');
$tachikoma->sink($tail);
$router->name('_router');
$router->debug_state($debug);
$router->drain;

sub usage {
    print "Usage:\n"
        . "fill_node [ --config=<path>     ]\n"
        . "          [ --host=<host>       ]\n"
        . "          [ --port=<port>       ]\n"
        . "          [ --stream=<stream>   ]\n"
        . "          [ --throttle          ]\n"
        . "          [ --debug             ]\n"
        . "          [ --help              ]\n"
        . "          <node path>\n";
    exit 1;
}
