#!/usr/bin/env perl
use strict;
use warnings;
use Tachikoma;
use Tachikoma::EventFrameworks::Select;
use Tachikoma::Nodes::Router;
use Tachikoma::Nodes::STDIO qw( TK_R );
use Tachikoma::Nodes::Tail;
use Tachikoma::Nodes::Socket qw( TK_SYNC );
use Getopt::Long qw( GetOptions );

my $home            = ( getpwuid $< )[7];
my @default_configs = (
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);
my $config_file = undef;
my $host        = 'localhost';
my $port        = 4230;
my $use_SSL     = undef;
my $throttle    = undef;
my $help        = undef;

my $r = GetOptions(
    'config=s'   => \$config_file,
    'host=s'     => \$host,
    'port=i'     => \$port,
    'use-ssl'    => \$use_SSL,
    'throttle:i' => \$throttle,
    'help'       => \$help,
);
my $to = $ARGV[0];

usage() if ( $help or not $r or not $to );
my $config = Tachikoma->configuration;
$config->load_config_file( $config_file ? $config_file : @default_configs );
Tachikoma->event_framework( Tachikoma::EventFrameworks::Select->new );

my $router = Tachikoma::Nodes::Router->new;
if ($throttle) {
    my $tail = Tachikoma::Nodes::Tail->filehandle( *STDIN, TK_R );
    my $tachikoma =
        Tachikoma::Nodes::Socket->inet_client( $host, $port, undef,
        $use_SSL );
    $tail->max_unanswered($throttle);
    $tail->size(0);
    $tail->owner($to);
    $tail->sink($tachikoma);
    $tail->on_timeout('die');
    $tail->on_ENOENT('die');
    $tail->on_EOF('close');
    $tachikoma->sink($tail);
    $router->drain($tail);
}
else {
    my $stdin = Tachikoma::Nodes::STDIO->filehandle( *STDIN, TK_R );
    my $tachikoma =
        Tachikoma::Nodes::Socket->inet_client( $host, $port, TK_SYNC,
        $use_SSL );
    $stdin->owner($to);
    $stdin->sink($tachikoma);
    $router->drain($stdin);
}

sub usage {
    print "Usage:\n"
        . "fill_node [ --config=<path>     ]\n"
        . "          [ --host=<host>       ]\n"
        . "          [ --port=<port>       ]\n"
        . "          [ --use-ssl           ]\n"
        . "          [ --throttle          ]\n"
        . "          [ --help              ]\n"
        . "          <node path>\n";
    exit 1;
}
