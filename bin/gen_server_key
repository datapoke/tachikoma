#!/usr/bin/env perl
# ----------------------------------------------------------------------
# gen_server_key
# ----------------------------------------------------------------------
#
use strict;
use warnings;
use Crypt::OpenSSL::RSA;

use version; our $VERSION = qv('v2.1.12');

umask 0077 or die "couldn't umask: $!";

my $home    = ( getpwuid $< )[7];
my $id_file = $ARGV[0] || "$home/.tachikoma/etc/id";
my $bits    = $ARGV[1] || 4096;
my $force   = $ARGV[2];
if ( -e $id_file ) {
    if ( not $force ) {
        die "$id_file exists!";
    }
    elsif ( $force eq 'ignore' ) {
        exit 0;
    }
}
my $rsa         = generate_key Crypt::OpenSSL::RSA($bits);
my $private_key = $rsa->get_private_key_string();
my $public_key  = $rsa->get_public_key_string();
open my $fh, '>', $id_file or die "couldn't open $id_file: $!";
print {$fh} <<"EOF";
#!/usr/bin/perl
# ----------------------------------------------------------------------
# tachikoma server rsa keypair
# ----------------------------------------------------------------------

use strict;
use warnings;
use Tachikoma;
use Sys::Hostname;

my \$config = Tachikoma->configuration;

# Set our ID
my \$hostname = hostname();
\$config->id("tachikoma\\\@\$hostname");

# Set your unencrypted private key here
\$config->private_key( q($private_key) );

\$config->public_keys->{ \$config->id } = {
    allow => {map {\$_=>1} qw(
        command
        client
        server
    )},
    public_key =>
q($public_key)};

1;
EOF
close $fh or die "couldn't close $id_file: $!";

print $public_key;
