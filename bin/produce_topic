#!/usr/bin/env perl
use strict;
use warnings;
use Tachikoma::Nodes::Topic;
use Time::HiRes;
use Getopt::Long qw( GetOptions );
use Digest::MD5  qw( md5 );

use version; our $VERSION = qv('v2.1.36');

my $home = ( getpwuid $< )[7];
Tachikoma->configuration->load_config_file(
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);

my $topic      = undef;
my $partition  = -1;
my $batch_size = 128;
my $mode       = 'values';
my $persist    = undef;
my $help       = undef;
my ( $r, $argv ) = GetOptions(
    'topic=s'     => \$topic,
    'partition=i' => \$partition,
    'batch=i'     => \$batch_size,
    'mode=s'      => \$mode,
    'persist'     => \$persist,
    'help'        => \$help,
);
$topic = shift @ARGV if ( not length $topic );
usage()              if ( $help or not $r or not length $topic );

my $broker     = Tachikoma::Nodes::Topic->new($topic);
my $partitions = $broker->get_partitions;
my $i          = $partition > -1 ? $partition : int rand 1_000_000;
my @lines      = ();
my $last_time  = 0;
my $total      = 0;
my @buf        = ();

$broker->persist( $persist ? 'cancel' : undef );

if ( $mode eq 'keys' ) {
    while ( my $key = <> ) {
        chomp $key;
        my $j = 0;
        $j += $_ for ( unpack 'C*', md5($key) );
        $j %= scalar @{$partitions};
        $buf[$j] ||= [];
        push @{ $buf[$j] }, $key;
        if ( @{ $buf[$j] } > $batch_size ) {
            my %batch = ();
            $batch{$_} = [1] for ( @{ $buf[$j] } );
            $Tachikoma::Now = time;
            $broker->send_kv( $j, \%batch )
                or die $broker->sync_error;
            $buf[$j] = [];
        }
    }
    for my $j ( 0 .. $#buf ) {
        next if ( not $buf[$j] or not @{ $buf[$j] } );
        my %batch = ();
        $batch{$_} = [1] for ( @{ $buf[$j] } );
        $broker->send_kv( $j, \%batch )
            or die $broker->sync_error;
        $buf[$j] = [];
    }
}
elsif ( $mode eq 'bytestream' ) {
    while ( my $size = sysread STDIN, $buf[0], 65536 ) {
        $broker->send_messages( $i, \@buf ) or die $broker->sync_error;
    }
}
else {
    while (1) {
        my $line = <>;
        push @lines, $line if ( defined $line );
        $Tachikoma::Right_Now = Time::HiRes::time;
        next
            if (defined $line
            and @lines < $batch_size
            and $Tachikoma::Right_Now - $last_time < 1 );
        $Tachikoma::Now = int $Tachikoma::Right_Now;
        $i              = ( $i + 1 ) % @{$partitions} if ( $partition < 0 );
        while ( @lines and not $broker->send_messages( $i, \@lines ) ) {
            print {*STDERR} $broker->sync_error;
            sleep 1;
        }
        last if ( not defined $line or not @lines );
        @lines     = ();
        $last_time = $Tachikoma::Right_Now;
    }
}

print "ok\n";

sub usage {
    print "Usage:\n"
        . "produce_topic   --topic=<name>\n"
        . "              [ --partition=<int>   ]\n"
        . "              [ --batch=<int>       ]\n"
        . "              [ --mode=<string>     ]\n"
        . "              [ --persist           ]\n"
        . "    # valid modes: keys values bytestream\n";
    exit 1;
}
