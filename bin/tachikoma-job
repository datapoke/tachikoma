#!perl
# ----------------------------------------------------------------------
# tachikoma job
# ----------------------------------------------------------------------
#

use strict;
use warnings;
use Tachikoma::Job;
use Tachikoma::Nodes::Router;
use Tachikoma::Nodes::Responder;
use Tachikoma::Nodes::Echo;
use Tachikoma::Nodes::FileHandle qw( TK_R TK_SYNC );
use Tachikoma::Config qw( load_module );

use version; our $VERSION = qv('v2.0.192');

my $home            = ( getpwuid $< )[7];
my @default_configs = (
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);
my ( $config_file, $class, $name, $arguments ) = @ARGV;
usage() if ( not $class );
srand;
my $config = Tachikoma->configuration;
$config->load_config_file( $config_file ? $config_file : @default_configs );
Tachikoma->load_event_framework;

$Tachikoma::Now       = time;
$Tachikoma::Right_Now = $Tachikoma::Now;
open my $child_fh, q(+<&=), 5 or usage();
my $connector =
    Tachikoma::Nodes::FileHandle->filehandle( $child_fh, TK_R | TK_SYNC );
my $router    = Tachikoma::Nodes::Router->new;
my $responder = Tachikoma::Nodes::Responder->new;
my $echo      = Tachikoma::Nodes::Echo->new;
$connector->name('_parent');
$connector->type('job');
$connector->on_EOF('shutdown');
$router->name('_router');
$router->type('job');
$responder->name('_responder');
$responder->sink($router);
$echo->name('_return_to_sender');
$echo->sink($router);

load_module($class);
my $job = $class->new;
$job->connector($connector);
$job->router($router);
$job->name($name);
$job->arguments($arguments);
$job->type($class);
$job->pid($$);
local $0 = $name;
$job->initialize_graph;

$router->drain;

sub usage {
    print {*STDERR} "ERROR: this program can only be run by tachikoma\n";
    exit 1;
}
