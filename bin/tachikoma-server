#!/usr/bin/env tachikoma

if (<_C> > 0) {
    for i (1 .. <_C>) {
        if ([var <i>] =~ '^--([^=]+)=(.*)$') {
            var <_1> = <_2>;
        }
        elsif ([var <i>] =~ '^--([^=]+)$') {
            var <_1> = 1;
        }
        else {
            log ("ERROR: couldn\'t parse argument:" [var <i>]);
            exit 1;
        }
    }
}
if ([var help]) {
    print 'Usage:
tachikoma-server [ --debug=<level>   ]
                 [ --daemon=[yes|no] ]
                 [ --startup=<file>  ]
                 [ --use-ssl         ]
                 [ --profile         ]
                 [ --help            ]
';
    exit;
}
if ([var use-ssl]) {
    env TKSSL=true;
}
if ([var profile]) {
    env NYTPROF=addpid=1:file=/volumes/tachikoma/nytprof.out;
    env PERL5OPT=-d:NYTProf;
}
if ([var debug]) {
    debug_state _command_interpreter <debug>;
    debug_state _router              <debug>;
} else {
    var debug = 0;
}
if (! [var startup]) {
    var startup = startup.txt;
}
if ([var daemon] eq "no") {
    initialize [var name];
    open_log_file;
} else {
    daemonize [var name];
}
listen_inet
log "arguments: <@>";
log "running <startup>"
make_node JobController jobs
command_node jobs start_job Inet_AtoN
include <startup>
