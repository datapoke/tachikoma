#!perl
# ----------------------------------------------------------------------
# TopicTop client
# ----------------------------------------------------------------------
#

use strict;
use warnings;
use Tachikoma::EventFrameworks::Select;
use Tachikoma::Nodes::Router;
use Tachikoma::Nodes::Callback;
use Tachikoma::Nodes::Socket;
use Tachikoma::Nodes::STDIO qw( TK_R TK_SYNC );
use Tachikoma::Nodes::TopicTop;
use Tachikoma::Message qw( PAYLOAD );
use Getopt::Long       qw( GetOptions );

use version; our $VERSION = qv('v2.1.12');

my $home = ( getpwuid $< )[7];
Tachikoma->configuration->load_config_file(
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);

my $help          = undef;
my $list_fields   = undef;
my $select_fields = undef;
my $host          = 'localhost';
my $port          = 4394;
my $use_SSL       = undef;
my %where         = ();
my %where_not     = ();
my %width         = ();
my $sort          = '_distance';
my $threshold     = undef;
my $delay         = 4;

my $r = GetOptions(
    'host=s'      => \$host,
    'port=i'      => \$port,
    'use-ssl'     => \$use_SSL,
    'select=s'    => \$select_fields,
    'where=s'     => \%where,
    'where_not=s' => \%where_not,
    'width=s'     => \%width,
    'sort=s'      => \$sort,
    'threshold=f' => \$threshold,
    'delay=f'     => \$delay,
    'list_fields' => \$list_fields,
    'help'        => \$help,
);
usage() if ( $help or not $r );
$threshold //= 0;

if ( not $select_fields ) {
    $select_fields = join q(,), qw(
        hostname consumer p_offset cache
        recv_rate direction2 send_rate msg_sent msg_rate
        msg_unanswered max_unanswered distance eta
    );
}

Tachikoma->event_framework( Tachikoma::EventFrameworks::Select->new );

my $topic_top = Tachikoma::Nodes::TopicTop->new;
list_fields() if ($list_fields);

my $router    = Tachikoma::Nodes::Router->new;
my $stdin     = Tachikoma::Nodes::STDIO->filehandle( *STDIN, TK_R );
my $get_input = Tachikoma::Nodes::Callback->new;
my $tachikoma =
    Tachikoma::Nodes::Socket->inet_client( $host, $port, undef, $use_SSL );
my $stdout = Tachikoma::Nodes::STDIO->filehandle( *STDOUT, TK_SYNC );

$stdin->sink($get_input);
$get_input->callback(
    sub {
        my $message = shift;
        my $key     = $message->[PAYLOAD];
        if ( $key eq 'q' ) {
            $router->shutdown_all_nodes;
            return;
        }
        elsif ( $key eq 'c' ) {
            $topic_top->sort_by('cache_size');
        }
        elsif ( $key eq 'd' ) {
            $topic_top->sort_by('_distance');
        }
        elsif ( $key eq 'm' ) {
            $topic_top->sort_by('_msg_rate');
        }
        elsif ( $key eq 'r' ) {
            $topic_top->sort_by('_recv_rate');
        }
        elsif ( $key eq 's' ) {
            $topic_top->sort_by('_send_rate');
        }
        elsif ( $key =~ m{\d} ) {
            $topic_top->delay( $key || 10 );
            return;
        }
        $topic_top->fire;
        return;
    }
);
$tachikoma->on_EOF('die');
$tachikoma->sink($topic_top);

$topic_top->name('_topic_top');
for my $field ( keys %width ) {
    $topic_top->fields->{$field}->{size} = $width{$field};
}
$topic_top->select_fields($select_fields);
$topic_top->where( \%where );
$topic_top->where_not( \%where_not );
$topic_top->sort_by($sort);
$topic_top->threshold($threshold);
$topic_top->delay($delay);
$topic_top->sink($stdout);

$stdout->on_EOF('ignore');

## no critic (RequireCheckedSyscalls)
system '/bin/stty', '-echo', 'cbreak';
$router->name('_router');
$router->drain;

sub list_fields {
    print join( q( ), sort keys %{ $topic_top->fields } ), "\n";
    exit 1;
}

sub usage {
    print "Usage:\n"
        . "topic_top [ --host=<host>                              ]\n"
        . "          [ --port=<port>                              ]\n"
        . "          [ --use-ssl                                  ]\n"
        . "          [ --select=<fields>                          ]\n"
        . "          [ --where <field>=<regex>                    ]\n"
        . "          [ --where_not <field>=<regex>                ]\n"
        . "          [ --width <field>=<size>                     ]\n"
        . "          [ --sort=<field>                             ]\n"
        . "          [ --threshold=<seconds>                      ]\n"
        . "          [ --delay=<seconds>                          ]\n"
        . "          [ --list_fields                              ]\n"
        . "          [ --help                                     ]\n";
    exit 1;
}
