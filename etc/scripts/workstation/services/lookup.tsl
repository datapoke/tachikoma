#!/usr/bin/env /usr/local/bin/tachikoma
var name = lookup;
include services/daemonize.tsl

var start = 0;
var count = 8;
var end   = <start> + <count> - 1;

make_node Tee images
for i ( <start> .. <end> ) {
    command jobs start_job CommandInterpreter images<i>;
    cd images<i>;
        connect_hub;

        # indb
        make_node ConsumerBroker images.indb:consumer   --broker=<broker>
                                                        --topic=images.indb
                                                        --partition_id=<i>
                                                        --default_offset=start;
        make_node Table          images.indb:table      --num_partitions=<count>
                                                        --window_size=86400
                                                        --num_buckets=2;
        connect_edge images.indb:consumer images.indb:table;

        # ondisk
        make_node ConsumerBroker images.ondisk:consumer --broker=<broker>
                                                        --topic=images.ondisk
                                                        --partition_id=<i>
                                                        --default_offset=start
                                                        --max_unanswered=1024;
        make_node Lookup         images:lookup          images.indb:table;
        make_node Topic          images.orphans         <broker>;

        topic_probe;
        insecure;
        on images.orphans READY {
            connect_edge images:lookup          images.orphans;
            connect_node images.ondisk:consumer images:lookup;
            secure 3;
        };
    cd ..;
    connect_node images images<i>;
}

listen_inet localhost:<root_port>
secure 3
