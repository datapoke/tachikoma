#!/usr/bin/env /usr/local/bin/tachikoma

func main {
    local n = <1>;
    local h = <n> / 2;
    matgen <n> a;
    matgen <n> b;
    mul <n> a b c;
    print ([var "r:<h>:<h>"]"\n");
};

func matgen {
    local count  = <1>;
    local prefix = <2>;
    local tmp    = 1.0 / <count> / <count>;
    # print "tmp: <tmp>\n";
    for i (0 .. (<count> - 1)) {
        for j (0 .. (<count> - 1)) {
            var "<prefix>:<i>:<j>" = (<tmp> * (<i> - <j>) * (<i> + <j>));
            # print ("<prefix>:<i>:<j>" = [var "<prefix>:<i>:<j>"]"\n");
        };
    };
};

func mul {
    local count = <1>;
    local a     = <2>;
    local b     = <3>;
    local c     = <4>;
    transpose <count> <b> <c>;
    for i (0 .. (<count> - 1)) {
        for j (0 .. (<count> - 1)) {
            var sum = 0;
            for k (0 .. (<count> - 1)) {
                # print ("<sum> +=" [var "<a>:<i>:<k>"] "*" [var "<c>:<j>:<k>"]"\n");
                sum += ([var "<a>:<i>:<k>"] * [var "<c>:<j>:<k>"]);
            };
            var "r:<i>:<j>" = <sum>;
            # print "r:<i>:<j> = <sum>\n";
        };
    };
};

func transpose {
    local count = <1>;
    local a     = <2>;
    local b     = <3>;
    for i (0 .. (<count> - 1)) {
        for j (0 .. (<count> - 1)) {
            var "<b>:<i>:<j>" = [var "<a>:<j>:<i>"];
            # print ("<a>:<j>:<i>" = [var "<a>:<j>:<i>"]"\n");
        };
    };
};

var count = 0;
if (<_C> > 0) {
    count = <1>;
};
if (! <count>) {
    print "enter an even number: ";
    count = [read];
};
if (! <count>) {
    count = 20;
};
main <count>;
