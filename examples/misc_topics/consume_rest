#!/usr/bin/env perl
use strict;
use warnings;
use HTTP::Request::Common qw(GET);
use JSON -support_by_pp;
use LWP::UserAgent;

my ( $server, $topic, $offset, $count ) = @ARGV;
die "ERROR: no topic\n" if not($topic);
$offset    //= 'start';
$count     //= 1024;
my $json = JSON->new;
my $ua   = LWP::UserAgent->new;
$ua->timeout(60);
push( @{ $ua->requests_redirectable }, 'GET' );
$server =~ s{$}{/cgi-bin/tail.cgi} if ( $server !~ m{/} );
$server =~ s{^}{http://}           if ( $server !~ m{^https?://} );
my $url = join q(/), $server, $topic, $offset, $count;
CONSUME: while (1) {
    my $req = GET $url;
    my $res = $ua->request($req);
    if ( $res->is_success ) {
        my $batch = $json->decode( $res->content );
        for my $record ( @{ $batch->{payload} } ) {
            print $record;
        }
        last if ( $url eq $batch->{next_url} );
        $url = $batch->{next_url};
    }
    else {
        my $output = $res->as_string;
        print STDERR $output;
        exit 1;
    }
}
