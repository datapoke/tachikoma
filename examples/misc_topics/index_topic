#!/usr/bin/env perl
use strict;
use warnings;
use Tachikoma::Nodes::ConsumerBroker;
use Tachikoma::Message qw( FROM ID STREAM );

my $home = ( getpwuid $< )[7];
Tachikoma->configuration->load_config_file(
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);

my $topic      = $ARGV[0] or die "usage: index_topic <topic> [ <group> ]\n";
my $group      = $ARGV[1] // "indexer";
my $consumer   = Tachikoma::Nodes::ConsumerBroker->new( $topic, $group );
my $last_print = 0;
my $total      = 0;
$consumer->default_offset('start');
while (1) {
    my $messages = $consumer->fetch(
        sub {
            my ( $self, $message ) = @_;
            $self->{cache}->{ $message->[STREAM] } =
                ( split( ':', $message->[ID], 2 ) )[0];
        }
    );
    if ( $consumer->sync_error ) {
        print STDERR time, ' ', $consumer->sync_error;
    }
    else {
        $total += scalar(@$messages);
        my $time = time;
        if ( $time - $last_print > 0 ) {
            print time, " received: $total\n";
            $last_print = $time;
            $total      = 0;
        }
    }
}
