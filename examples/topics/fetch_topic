#!/usr/bin/perl
# ----------------------------------------------------------------------
# fetch_topic
# ----------------------------------------------------------------------
#

use strict;
use warnings;
use Tachikoma::Nodes::ConsumerBroker;
use Tachikoma::Nodes::Table;
use JSON -support_by_pp;

my $home = ( getpwuid $< )[7];
Tachikoma->configuration->load_config_file(
    "$home/.tachikoma/etc/tachikoma.conf",
    '/usr/local/etc/tachikoma.conf',
);

my ( $topic, $field, $key, $verbose ) = @ARGV;
die "no topic\n" if ( not length $topic );
die "no field\n" if ( not length $field );
my $consumer = Tachikoma::Nodes::ConsumerBroker->new($topic);
my $table    = Tachikoma::Nodes::Table->new;
$table->host('localhost');
$table->port(6100);
$table->field("$topic.$field:table");
my $json = JSON->new;
$json->canonical(1);
$json->pretty(1);
$json->allow_blessed(1);
$json->convert_blessed(0);
my $output  = undef;
my $value   = undef;
my $payload = $table->fetch($key);

if ($payload) {
    if ( ref $payload ) {
        $value = $payload;
    }
    else {
        my ( $partition, $offset ) = split m{:}, $payload, 2;
        $value = $consumer->fetch_offset( $partition, $offset );
    }
}
if ( ref $value eq 'Tachikoma::Message' ) {
    $output = $verbose ? &dump_message($value) : $value->payload;
}
else {
    $output = $value;
}
if ( ref $output ) {
    print $json->utf8->encode($output);
}
elsif ( defined $output ) {
    print $output;
}

sub dump_message {
    my $message = shift;
    return {
        type      => $message->type_as_string,
        timestamp => $message->timestamp,
        partition => $message->from,
        offset    => $message->id,
        key       => $message->stream,
        payload   => $message->payload
    };
}
